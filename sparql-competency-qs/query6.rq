# If a particular dataset has failed bias assertions, return all datasets created by the same entity
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX airo: <https://w3id.org/airo#>
PREFIX earl: <http://www.w3.org/ns/earl#>
PREFIX dqv: <http://www.w3.org/ns/dqv#>
PREFIX terms: <http://purl.org/dc/terms/>

PREFIX : <https://example.org/ex#>

SELECT ?dataset (GROUP_CONCAT(DISTINCT ?relatedDataset; separator=", ") AS ?relatedDatasets)
WHERE {
    {
        SELECT DISTINCT ?dataset ?creator
        WHERE {
            ?dataset rdf:type dcat:Dataset ;
                airo:hasDocumentation ?doc .
            ?doc rdf:type :BiasAudit ;
                :hasAssertion ?biasassertion .
            ?biasassertion earl:result ?testresult .
            ?testresult earl:outcome ?testoutcome .
            ?testoutcome rdf:type ?type .
            FILTER (?type IN (:FloatFail , :IntFail , earl:Fail))

            ?dataset terms:creator ?creator .
        }
    }

    ?relatedDataset rdf:type dcat:Dataset ;
        terms:creator ?creator .
}
GROUP BY ?dataset
